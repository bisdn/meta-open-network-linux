#@TYPE: Machine
#@NAME: generic armel iProc ONL image

#@DESCRIPTION: Machine configuration for generic armel iProc ONL image

PREFERRED_PROVIDER_virtual/kernel ?= "linux-yocto-onl"
PREFERRED_VERSION_linux-yocto-onl ?= "6.6%"

KMACHINE:generic-armel-iproc = "armel-iproc"
KERNEL_FEATURES += "bsp/armel-iproc"

DEFAULTTUNE ?= "cortexa9"
require conf/machine/include/qemu.inc
require conf/machine/include/arm/armv7a/tune-cortexa9.inc
require conf/machine/include/onl.inc

MACHINE_FEATURES += "pcbios"
MACHINE_FEATURES:remove = "alsa"

KERNEL_FEATURES:remove = "features/debug/printk.scc features/kernel-sample/kernel-sample.scc features/taskstats/taskstats.scc"

# https://git.yoctoproject.org/poky/tree/meta/classes/kernel-fitimage.bbclass
KERNEL_CLASSES += "kernel-fitimage"

KERNEL_IMAGETYPE = "fitImage"
UBOOT_ENTRYPOINT="0x61008000"
UBOOT_RD_LOADADDRESS = "0x00000000"
UBOOT_RD_ENTRYPOINT = "0x00000000"
# AS4610 U-Boot does not support sha256
FIT_HASH_ALG = "sha1"
KERNEL_EXTRA_ARGS += "LOADADDR=${UBOOT_ENTRYPOINT}"

PREFERRED_PROVIDER_virtual/dtb = "generic-armel-iproc-dtbs"

BISDN_ARCH = "u-boot-arch"

ONL_PLATFORM_SUPPORT = " \
    arm-accton_as4610_30-r0 \
    arm-accton_as4610_54-r0 \
"

MACHINE_EXTRA_RDEPENDS += " \
    bcm591xx-poe-mcu-mod \
    packagegroup-onl-platform-arm-accton-as4610 \
    platform-onl-init \
"

UBOOT_CONFIG = "sandbox"
UBOOT_CONFIG[sandbox] = "sandbox_defconfig"

PREFERRED_RPROVIDER_u-boot-default-env = "platform-onl-init"
